_realname=ruby
pkgbase=mingw-w64-ruby-head
pkgname="${MINGW_PACKAGE_PREFIX}-ruby-head"
pkgver=r20210615
pkgver() {
  echo r`date +%Y%m%d`
}
pkgrel=1
pkgdesc="An object-oriented language for quick and easy programming (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
url="https://www.ruby-lang.org/en"
license=("BSD, custom")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gmp"
         "${MINGW_PACKAGE_PREFIX}-libffi"
         "${MINGW_PACKAGE_PREFIX}-libyaml"
         "${MINGW_PACKAGE_PREFIX}-openssl>=1.1.1"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')
source=("https://cache.ruby-lang.org/pub/ruby/snapshot/snapshot-master.tar.xz"
        ruby-configure-coroutine-mingw32.patch
        ruby-configure-mingw32-ucrt.patch
        ruby-date-mingw32-ucrt-timezone.patch
        ruby-i386-mingw32-ucrt-undef.patch
        ruby-mingw32-ucrt.patch
        0002-Don-t-add-an-exe-file-manifest.patch
        ruby.ico
        rubyw.ico)
noextract=("snapshot-master.tar.xz")

# To update SHA256 hashs run: ridk exec sh -c "updpkgsums PKGBUILD"
# and set first entry to 'SKIP'

sha256sums=('SKIP'
            'ed04463388069393f2ee3b759e87f366cae7a6b2c3fd8adfcaf53fb1f245d3f1'
            'e82a008ae0ddade1e020fd7632c752eee31e3debd747cda4e3e8c4a4621390bf'
            '483025e7742387aa353fe223f85d84f84099d12979bb99aadabc57fe6eae84d6'
            '0d535322829a7e52be5ca62c04f4e21467af121342545b6a9007250a582f1e48'
            '4a97c2513ad74b626884c75cc31555c9a49548b7c775ecd225e7e921059dfd05'
            'da079dbfdc3d4e1b976ed15d58244ed6e0df201ec829bead578fb4a7177b9cab'
            '6c80ba2ca49840e387a08b7fedc6e7acd298ac2ec853155209efb2af20397b22'
            'c30fb04ac0e88ba634daaa811a81ea5a75027646617b895acd14df9518a4a55a')

prepare() {
  bsdtar -xf snapshot-master.tar.xz || true
  cd ${srcdir}/snapshot-master
  patch -p1 -i ${srcdir}/ruby-configure-coroutine-mingw32.patch
  patch -p1 -i ${srcdir}/ruby-configure-mingw32-ucrt.patch
  patch -p1 -i ${srcdir}/ruby-date-mingw32-ucrt-timezone.patch
  patch -p1 -i ${srcdir}/ruby-i386-mingw32-ucrt-undef.patch
  patch -p1 -i ${srcdir}/ruby-mingw32-ucrt.patch
  patch -p1 -i ${srcdir}/0002-Don-t-add-an-exe-file-manifest.patch
  autoreconf -fi
}

build() {

  CPPFLAGS+=" -DFD_SETSIZE=2048"
  CFLAGS="-O3 -fno-fast-math -fstack-protector-strong"
  LDFLAGS="-pipe -s -fstack-protector-strong"

  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  cp ${srcdir}/*.ico .

  ../snapshot-master/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --with-out-ext=readline,pty,syslog

  make showflags main
  echo "Generate RI and HTML documentation"
  # Make rdoc outputs so many lines -> print dots only
  make rdoc html 2>&1 | ./miniruby -ne "STDERR.print '.'"
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  echo $PATH
  make -l "TESTOPTS=-q" btest test-basic
  # TODO: Full tests fail currently
  # make -l "TESTOPTS=-q" test-all || true
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -f GNUMakefile DESTDIR="${pkgdir}" install
  for script in gem; do
    install ${srcdir}/snapshot-master/bin/${script} \
      ${pkgdir}${MINGW_PREFIX}/bin/
  done
}
