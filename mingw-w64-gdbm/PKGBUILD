# Maintainer: MSP-Greg

_realname=gdbm
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.10
pkgrel=2
pkgdesc="GNU database library (mingw-w64)"
url="http://www.gnu.org/software/gdbm/"
license=('GPL3')
arch=('any')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gettext"
         "${MINGW_PACKAGE_PREFIX}-libiconv" )
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('staticlibs')

source=(ftp://ftp.gnu.org/gnu/gdbm/${_realname}-${pkgver}.tar.gz{,.sig}
        'gdbm-1.10-zeroheaders.patch'
        'gdbm-1.10-page_size.patch'
        'gdbm-win32-support.patch')

sha256sums=('23f8134c5b94bbfb06d756a6b78f074fba6e6028cf2fe01341d40b26db773441'
            'd85ff239579ee6812c0c7c11e08dba4f79fa9b629ddca3fcb5016d227f82c095'
            '82cacfdc6437520db382e54ee9a637bae843059b72def6efa1a91552f7c26715'
            '08a8423e88a45e698070a8bbb7723f6d04b619e3ea29447fb7359800ce1adc17'
            '1a7868d8619a7aa7f60b1e52283e92d1c7b6165ab5b84e75d4b805b28cf007d4')

PACKAGER=MSP-Greg

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/gdbm-1.10-zeroheaders.patch
  patch -p1 -i ${srcdir}/gdbm-win32-support.patch
  patch -p1 -i ${srcdir}/gdbm-1.10-page_size.patch

  autoreconf --install --force
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}

  ${srcdir}/${_realname}-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-static \
    --enable-shared \
    --enable-libgdbm-compat

  make
}

check() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make check
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install
}